# -*- mode: python -*-

Import("env")

env = env.Clone()

stdxEnv = env.Clone()
stdxEnv.AppendUnique(
    LINKFLAGS=[
        # If we have -static-libstdc++ on the link line, then we want
        # to injest the entire C++ runtime library into libstdx, not
        # just referenced symbols. If we don't have it. then having
        # this won't have any effect.
        '$LINK_WHOLE_ARCHIVE_LIB_START',

        # For whatever reason, '-lgcc -lgcc_eh' appears multiple times
        # on the link line when building with --static-libgcc. Without
        # this, we would get duplicate symbol errors because of that,
        # since we are pulling in the whole library unconditionally.
        "-Wl,-z,muldefs",
    ],
)

stdxEnv.Library(
    target='stdx',
    source=[
        'set_terminate_internals.cpp',
    ],
    LIBDEPS=[
        # There should be no linking dependencies upon any other libraries, for `libstdx`.
        # This library is a shim filling in for deficiencies in various standard library
        # implementations.  There should never be any link-time dependencies into mongo internals.

    ],
    SYSLIBDEPS=[
        # As above
    ],
    LIBS=[
        # As above
    ],
)

env.Benchmark(
    target='condition_variable_bm',
    source=[
        'condition_variable_bm.cpp',
    ],
    LIBDEPS=[
    ],
)

env.CppUnitTest(
    target='stdx_test',
    source=[
        'unordered_map_test.cpp'
    ],
    LIBDEPS=[
        '$BUILD_DIR/third_party/shim_abseil',
    ],
)

# The tests for `stdx::set_terminate` need to run outside of the mongo unittest harneses.
# The tests require altering the global `set_terminate` handler, which our unittest framework
# doesn't expect to have happen.  Further, the tests have to return successfully from a
# terminate condition which interacts poorly with the unittest framework.
#
# A set of dedicated binaries to each test case is actually the simplest way to accomplish
# robust testing of this mechanism.

# Needs to be a different test -- It has to have direct control over the `main()` entry point.
env.RegisterUnitTest(env.Program(
    target='set_terminate_dispatch_test',
    source=[
        'set_terminate_dispatch_test.cpp',
    ],
    LIBDEPS=[
        'stdx',
    ]
)[0])

# Needs to be a different test -- It has to have direct control over the `main()` entry point.
env.RegisterUnitTest(env.Program(
    target='set_terminate_from_main_die_in_thread_test',
    source=[
        'set_terminate_from_main_die_in_thread_test.cpp',
    ],
    LIBDEPS=[
        'stdx',
    ]
)[0])

# Needs to be a different test -- It has to have direct control over the `main()` entry point.
env.RegisterUnitTest(env.Program(
    target='set_terminate_from_thread_die_in_main_test',
    source=[
        'set_terminate_from_thread_die_in_main_test.cpp',
    ],
    LIBDEPS=[
        'stdx',
    ]
)[0])

# Needs to be a different test -- It has to have direct control over the `main()` entry point.
env.RegisterUnitTest(env.Program(
    target='set_terminate_from_thread_die_in_thread_test',
    source=[
        'set_terminate_from_thread_die_in_thread_test.cpp',
    ],
    LIBDEPS=[
        'stdx',
    ]
)[0])
